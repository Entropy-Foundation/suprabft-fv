#lang ivy1.8

include ubd_seq

instance round_t : ubd_seq
instance height_t : ubd_seq

instance process_index_t : iterable
relation is_good(N:process_index_t)

function leader(R:round_t) : process_index_t

# There are two ways to access a type's member. Example
# [block_t.parent(b, p)] is a relation that is set to true when [p] is the parent of [b].
# [block_t.get_parent(b)] is an action that gives us the parent of a valid block [b]. It
# uses the [if some] syntax to get a parent from the [parent] relation.
#
# These are used as per convenience in [algorithm.ivy] and other files. We have some
# invariants set up here that relate the two.
#
# Note that actions where validity can only be determined with global view information are
# placed there. For example, [global_view.qc_get_block] instead of [qc_t.get_block].
isolate block_t = {
    type this

    relation cstd(B:block_t)

    relation round(B:block_t, R:round_t)
    relation parent(B:block_t, P:block_t)
    relation ancestor(B:block_t, A:block_t)
    relation height(B:block_t, H:height_t)

    individual genesis : block_t

    after init {
        cstd(B) := B = genesis;

        round(B, R) := B = genesis & R = 0;
        parent(B, P) := B = genesis & P = genesis;
        ancestor(B, A) := B = genesis & A = genesis;
        height(B, H) := B = genesis & H = 0;
    }

    export action get_round(b:block_t) returns (r:round_t) = {
        require cstd(b);

        if some b_r:round_t. round(b, b_r) {
            r := b_r;
        }

        ensure round(b, r);
    }

    export action get_parent(b:block_t) returns (pB:block_t) = {
        require cstd(b);

        if some b_pB:block_t. parent(b, b_pB) {
            pB := b_pB;
        }

        ensure parent(b, pB);
    }

    export action get_height(b:block_t) returns (h:height_t) = {
        require cstd(b);

        if some b_h:height_t. height(b, b_h) {
            h := b_h;
        }

        ensure height(b, h);
    }

    export action block(r_c:round_t, pB:block_t) returns (b:block_t) = {
        require cstd(pB);
        require round(pB,R) -> R < r_c;
        assume ~cstd(b);

        #round(B, R) := round(B, R) | (B = b & R = r_c);
        #parent(B, P) := parent(B, P) | (B = b & P = pB);
        #ancestor(B, A) := ancestor(B, A) | (B = b & A = pB) | (B = b & ancestor(pB, A));
        height(B, H) := height(B, H) | B = b & H = height_t.next(get_height(pB));

        cstd(b) := true;
        
        round(b,r_c) := true;
        parent(b,pB) := true;
        ancestor(b,pB) := true;
        ancestor(b,B) := ancestor(pB,B) | B = pB;

    }

    invariant cstd(genesis)
    invariant round(B, 0) <-> B = genesis
    invariant parent(genesis, P) <-> P = genesis
    invariant ancestor(genesis, A) <-> A = genesis
    invariant height(B, 0) <-> B = genesis

    invariant parent(B, B) <-> B = genesis
    invariant ancestor(B, B) <-> B = genesis

    invariant round(B, R) -> cstd(B)
    invariant parent(B, P) -> cstd(B) & cstd(P)
    invariant ancestor(B, A) -> cstd(B) & cstd(A)
    invariant height(B, H) -> cstd(B)

    invariant cstd(B) -> exists R:round_t. round(B, R)
    relation parent_exists(B:block_t)
    definition parent_exists(B) = exists P:block_t. parent(B, P)
    invariant cstd(B) -> parent_exists(B)
    invariant cstd(B) -> exists H:height_t. height(B, H)

    invariant round(B, R1) & round(B, R2) -> R1 = R2
    invariant parent(B, P1) & parent(B, P2) -> P1 = P2
    invariant height(B, H1) & height(B, H2) -> H1 = H2

    invariant (parent(B,P) & round(B,Rb) & round(P,Rp) & B ~= genesis) -> Rp < Rb
    invariant (parent(B,P) & height(B,Hb) & height(P,Hp) & B ~= genesis) -> height_t.succ(Hp,Hb)
} with height_t, round_t

isolate ancestor_transitive_closure_block = {
    invariant [i1] block_t.parent(B, P) -> block_t.ancestor(B, P)
    invariant [i2] block_t.ancestor(A1, A2) & block_t.ancestor(A2, A3) -> block_t.ancestor(A1, A3)
    invariant [i4] block_t.ancestor(B, A1) & block_t.ancestor(B, A2) -> A1 = A2 | block_t.ancestor(A1, A2) | block_t.ancestor(A2, A1)
    relation inbetween(D:block_t, A:block_t)
    definition inbetween(D, A) = exists M:block_t. block_t.ancestor(D, M) & block_t.ancestor(M, A)
    invariant [i5] block_t.ancestor(D, A) & ~inbetween(D, A) -> (D = block_t.genesis & A = block_t.genesis) | block_t.parent(D, A)
    invariant [i3] block_t.parent(B,P) & P ~= block_t.genesis -> ~inbetween(B,P)
    invariant [i6] block_t.parent(B, P) -> (B = block_t.genesis & P = block_t.genesis) | forall B_h,P_h:height_t. block_t.height(B, B_h) & block_t.height(P, P_h) -> height_t.succ(P_h, B_h)
} with block_t, height_t

isolate parent_same_implications = {
    invariant ancestor_transitive_closure_block.inbetween(B1,B2) | ~ ancestor_transitive_closure_block.inbetween(B1,B2)

    invariant block_t.parent(B1, P) & block_t.parent(B2, P) & block_t.ancestor(B1, A) -> block_t.ancestor(B2, A)

    attribute macro_finder=false
} with block_t, ancestor_transitive_closure_block.i1, ancestor_transitive_closure_block.i2, ancestor_transitive_closure_block.i3, ancestor_transitive_closure_block.i4, ancestor_transitive_closure_block.inbetween, height_t

isolate parent_same_implications_height = {
    invariant ancestor_transitive_closure_block.inbetween(B1,B2) | ~ ancestor_transitive_closure_block.inbetween(B1,B2)
    #invariant block_t.parent(B1, P) & block_t.parent(B2, P) & block_t.height(B1, H) & B1 ~= block_t.genesis & B2 ~= block_t.genesis -> block_t.height(B2, H)
} with block_t, ancestor_transitive_closure_block, height_t

isolate proposal_n_t = {
    type this

    relation valid(P:proposal_n_t)

    relation block(P:proposal_n_t, B:block_t)

    individual genesis : proposal_n_t

    after init {
        valid(P) := P = genesis;

        block(P, B) := P = genesis & B = block_t.genesis;
    }

    export action get_block(p:proposal_n_t) returns (b:block_t) = {
        #require cstd(p);

        if some p_b:block_t. block(p, p_b) {
            b := p_b;
        }

        ensure block(p, b);
    }

    export action proposal_n(b:block_t) returns (p:proposal_n_t) = {
        require block_t.cstd(b);
        assume ~valid(p);

        if some p_old:proposal_n_t. block(p_old, b) {
            p := p_old;
        } else {
            block(P, B) := block(P, B) | (P = p & B = b);
        }

        valid(P) := valid(P) | P = p;
    }

    invariant valid(genesis)
    invariant block(genesis, B) <-> B = block_t.genesis

    invariant block(P, B) -> valid(P) & block_t.cstd(B)

    invariant valid(P) -> exists B:block_t. block(P, B)

    invariant block(P, B1) & block(P, B2) -> B1 = B2
    invariant block(P1, B) & block(P2, B) -> P1 = P2
} with block_t

isolate prepare_t = {
    type this

    relation valid(P:prepare_t)

    relation block(P:prepare_t, B:block_t)

    individual genesis : prepare_t

    after init {
        valid(P) := P = genesis;

        block(P, B) := P = genesis & B = block_t.genesis;
    }

    export action get_block(p:prepare_t) returns (b:block_t) = {
        require valid(p);

        if some p_b:block_t. block(p, p_b) {
            b := p_b;
        }

        ensure block(p, b);
    }

    export action prepare(b:block_t) returns (p:prepare_t) = {
        #require block_t.cstd(b);
        assume ~valid(p);

        if some p_old:prepare_t. block(p_old, b) {
            p := p_old;
        } else {
            block(P, B) := block(P, B) | (P = p & B = b);
        }

        valid(P) := valid(P) | P = p;
    }

    invariant valid(genesis)
    invariant block(genesis, B) <-> B = block_t.genesis

    invariant block(P, B) -> valid(P) & block_t.cstd(B)

    invariant valid(P) -> exists B:block_t. block(P, B)

    invariant block(P, B1) & block(P, B2) -> B1 = B2
    invariant block(P1, B) & block(P2, B) -> P1 = P2
} with block_t

isolate quorum_fplusone_t = {
    type this

    relation member(Q:quorum_fplusone_t, N:process_index_t)
}

isolate every_quorum_fplusone_good = {
    axiom exists N:process_index_t. is_good(N) & quorum_fplusone_t.member(Q, N)
}

isolate quorum_t = {
    type this

    relation member(Q:quorum_t, N:process_index_t)

    individual genesis : quorum_t
}

isolate genesis_member = {
    axiom quorum_t.member(quorum_t.genesis, N)
}

isolate every_quorum_good = {
    axiom exists N:process_index_t. is_good(N) & quorum_t.member(Q, N)
}

isolate quorum_intersection_axiom = {
    axiom exists N:process_index_t. is_good(N) & quorum_t.member(Q1, N) & quorum_t.member(Q2, N)
}

isolate qc_t = {
    type this

    relation block(QC:qc_t, B:block_t)
    relation prepare(QC:qc_t, P:prepare_t)
    relation quorum(QC:qc_t, Q:quorum_t)

    individual genesis : qc_t

    after init {
        block(genesis, B) := B = block_t.genesis;
        prepare(genesis, P) := P = prepare_t.genesis;
        quorum(genesis, Q) := Q = quorum_t.genesis;
    }

    invariant block(genesis, B) <-> B = block_t.genesis
    invariant prepare(genesis, P) <-> P = prepare_t.genesis
    invariant quorum(genesis, Q) <-> Q = quorum_t.genesis
}

isolate timeout_t = {
    type this

    relation was_constructed(T:timeout_t)

    relation round(T:timeout_t, R:round_t)
    relation qc(T:timeout_t, QC:qc_t)

    after init {
        was_constructed(T) := false;

        round(T, R) := false;
        qc(T, QC) := false;
    }

    export action get_round(t:timeout_t) returns (r:round_t) = {
        require was_constructed(t);

        if some t_r:round_t. round(t, t_r) {
            r := t_r;
        }

        ensure round(t, r);
    }

    export action timeout(r:round_t, q:qc_t) returns (t:timeout_t) = {
        assume ~was_constructed(t);

        if some t_old:timeout_t. round(t_old, r) & qc(t_old, q) {
            t := t_old;
        } else {
            round(T, R) := round(T, R) | (T = t & R = r);
            qc(T, QC) := qc(T, QC) | (T = t & QC = q);
        }

        was_constructed(T) := was_constructed(T) | T = t;
    }

    invariant round(T, R) -> was_constructed(T)
    invariant qc(T, QC) -> was_constructed(T)

    invariant was_constructed(T) -> exists R:round_t. round(T, R)
    invariant was_constructed(T) -> exists QC:qc_t. qc(T, QC)

    invariant round(T, R1) & round(T, R2) -> R1 = R2
    invariant qc(T, QC1) & qc(T, QC2) -> QC1 = QC2
}

isolate tc_fplusone_t = {
    type this

    relation round(TC:tc_fplusone_t, R:round_t)
    relation timeout(TC:tc_fplusone_t, T:timeout_t)
    relation quorum(TC:tc_fplusone_t, Q:quorum_fplusone_t)
}

isolate tc_t = {
    type this

    relation round(TC:tc_t, R:round_t)
    relation timeout(TC:tc_t, T:timeout_t)
    relation quorum(TC:tc_t, Q:quorum_t)
}

isolate proposal_f_t = {
    type this

    relation was_constructed(P:proposal_f_t)

    relation block(P:proposal_f_t, B:block_t)
    relation qc(P:proposal_f_t, QC:qc_t)
    relation tc(P:proposal_f_t, TC:tc_t)

    after init {
        was_constructed(P) := false;

        block(P, B) := false;
        qc(P, QC) := false;
        tc(P, TC) := false;
    }

    export action get_block(p:proposal_f_t) returns (b:block_t) = {
        require was_constructed(p);

        if some p_b:block_t. block(p, p_b) {
            b := p_b;
        }

        ensure block(p, b);
    }

    export action proposal_f(b:block_t, q:qc_t, t:tc_t) returns (p:proposal_f_t) = {
        require block_t.cstd(b);
        assume ~was_constructed(p);

        if some p_old:proposal_f_t. block(p_old, b) & qc(p_old, q) & tc(p_old, t) {
            p := p_old;
        } else {
            block(P, B) := block(P, B) | (P = p & B = b);
            qc(P, QC) := qc(P, QC) | (P = p & QC = q);
            tc(P, TC) := tc(P, TC) | (P = p & TC = t);
        }

        was_constructed(P) := was_constructed(P) | P = p;
    }

    invariant block(P, B) -> was_constructed(P) & block_t.cstd(B)
    invariant qc(P, QC) -> was_constructed(P)
    invariant tc(P, TC) -> was_constructed(P)

    invariant was_constructed(P) -> exists B:block_t. block(P, B)
    invariant was_constructed(P) -> exists QC:qc_t. qc(P, QC)
    invariant was_constructed(P) -> exists TC:tc_t. tc(P, TC)

    invariant block(P, B1) & block(P, B2) -> B1 = B2
    invariant qc(P, QC1) & qc(P, QC2) -> QC1 = QC2
    invariant tc(P, TC1) & tc(P, TC2) -> TC1 = TC2
} with block_t
